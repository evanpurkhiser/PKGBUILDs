# Maintainer: Evan Purkhiser <evanpurkhiser@gmail.com>

pkgname=setcolors-git
pkgdesc="Allows you to set the linux VT101 default color palette"
license=('MIT')
url="https://github.com/evanpurkhiser/linux-vt-setcolors"
pkgver=62.4e1686f
pkgrel=1

provides=('setcolors')
conflicts=('setcolors')

source=("$pkgname::git+$url.git")
md5sums=('SKIP')
depends=('glibc')
makedepends=('git')
arch=('i686' 'x86_64')

pkgver() {
	cd "$srcdir/$pkgname"
	git describe --long --abbrev=7 | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/$pkgname"
	make
}

package() {
	cd "$srcdir/$pkgname"

	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm 644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
