# Maintainer: Evan Purkhiser <evanpurkhiser@gmail.com>

pkgname=keyfinder-cli-git
pkgdesc="A DJ friendly tool for estimating the musical key of an audio file"
license=('GPL3')
url="https://github.com/evanpurkhiser/keyfinder-cli"
pkgver=40.91ec8bb
pkgrel=1

provides=('keyfinder-cli')
conflicts=('keyfinder-cli')

source=("$pkgname::git+$url.git")
md5sums=('SKIP')
depends=('libkeyfinder' 'ffmpeg')
makedepends=('git' 'cmake')
arch=('i686' 'x86_64')

pkgver() {
	cd "$srcdir/$pkgname"
	git describe --long --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/$pkgname"
	cmake -S . -B build -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=None
	cmake --build build
}

package() {
	cd "$srcdir/$pkgname"

	DESTDIR="${pkgdir}" cmake --install build
	install -Dm 644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
