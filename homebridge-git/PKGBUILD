# Maintainer: ahrs <Forward dot to at hotmail dot co dot uk>
# Maintainer: Evan Purkhiser <evanpurkhiser@gmail.com>

pkgname=homebridge-git
pkgdesc='HomeKit support for the impatient'
license=('apache')
url='https://github.com/nfarina/homebridge'
pkgver=929.71ede01
pkgrel=1

depends=('nodejs>=4.3.2' 'avahi')

# Python is needed for node-gyp for building native modules
source=("$pkgname::git+$url" 'homebridge-system.service' 'homebridge-user.service' 'homebridge.install')
install='homebridge.install'
md5sums=('SKIP'
	'09623f14272d5778d474b9575643a887'
	'3c257887eac25cdcc27a4d8ed389f0f5'
	'455ddd190ffbf38eeb442db0abd7a469')

makedepends=('git' 'npm' 'python')
provides=('homebridge')
conflicts=('homebridge')
arch=('any')

pkgver() {
	cd "$srcdir/$pkgname"
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
	cd "$srcdir"

	npm pack $pkgname/
	npm install --cache ../cache --user root -g --prefix "$pkgdir/usr" *.tgz

	install -D -m644 "$srcdir/$pkgname/LICENSE" -t "$pkgdir/usr/share/licenses/$homebridge"
	install -D -m644 "$srcdir/homebridge-system.service" "$pkgdir/usr/lib/systemd/system/homebridge.service"
	install -D -m644 "$srcdir/homebridge-user.service" "$pkgdir/usr/lib/systemd/user/homebridge.service"
}
