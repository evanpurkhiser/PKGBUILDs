# Maintainer: Evan Purkhiser <evanpurkhiser@gmail.com>

gopath_root="go.evanpurkhiser.com"
pkgname=waitress-git
pkgdesc="Waitress is a small server that quickly serves up your files with style."
license=('MIT')
url="https://github.com/evanpurkhiser/waitress"
pkgver=39.c0711cf
pkgrel=1

source=("$pkgname::git+$url.git" "waitress.service" "waitress.conf")
md5sums=('SKIP' '2fbcf107949559a5c8f4ee0d8425a598' 'e1d0d2d8eefe3c4189f46886198c2234')
makedepends=('git' 'go' 'zip' 'dep' 'yarn')
optdepends=('systemd')
arch=('any')
backup=('etc/waitress.conf')

# We have to disable stripping or the appended binary data from rice append
# will be removed.
options=(!strip)

pkgver() {
	cd "$srcdir/$pkgname"

	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd "$srcdir/$pkgname"
	export PATH="$HOME/go/bin:$PATH"

	make
}

package() {
	install -D -m0755 "$srcdir/$pkgname/dist/waitress" "$pkgdir/usr/bin/waitress"
	install -D -m0644 "$srcdir/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -D -m0644 "$srcdir/waitress.service" "$pkgdir/usr/lib/systemd/system/waitress.service"
	install -D -m0644 "$srcdir/waitress.conf" "$pkgdir/etc/waitress.conf"
}
