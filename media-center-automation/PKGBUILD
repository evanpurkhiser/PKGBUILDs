# Maintainer: Evan Purkhiser <evanpurkhiser@gmail.com>

pkgname=media-center-automation
pkgdesc="Handle media-center power automation"
license=('MIT')
url="https://github.com/EvanPurkhiser/media-center-automation"
pkgver=28.55cca85
pkgrel=1

source=("$pkgname::git+$url")
md5sums=('SKIP')
makedepends=('git')
depends=('systemd' 'kodi' 'lirc' 'python2' 'python2-dbus' 'python2-gobject2' 'dbus-glib')
arch=('i686' 'x86_64')

pkgver() {
    cd "$srcdir/$pkgname"
    echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/$pkgname"

  # Bluetooth power listener
  install -D -m0755 "bt-power-listener/bt-power-listener"         "$pkgdir/usr/bin/bt-power-listener"
  install -D -m0644 "bt-power-listener/bt-power-listener.service" "$pkgdir/usr/lib/systemd/system/bt-power-listener.service"
  install -D -m0644 "bt-power-listener/bt_power_listener.conf"    "$pkgdir/etc/dbus-1/system.d/bt_power_listener.conf"

  # Lirc remotes
  install -D -m0644 "remotes/SMSL_RC36.conf"      "$pkgdir/etc/lirc/lircd.conf.d/SMSL_RC36.conf"
  install -D -m0644 "remotes/LG_AKB73975711.conf" "$pkgdir/etc/lirc/lircd.conf.d/LG_AKB73975711.conf"

  # NES30 bluetooth rules
  install -D -m0644 "10-nes30-joystick.rules" "$pkgdir/etc/udev/rules.d/10-nes30-joystick.rules"
}
